# Appendix: Childcare in British Columbia

This document is a not-for-publication appendix to Friesen et al (2025).
It describes the results of our analysis of 2011 GSS data (Statistics
Canada 2012) on childcare in Canada.  Our primary interest is to describe
childcare usage by parents of 5 year olds in British Columbia during this
time period.

A summary of the main results is as follows:

- Roughly 40% of parents in our target group used childcare regularly.
  -  This usage rate was much lower than in Quebec, but only slightly less than
     in the rest of Canada (ROC).
- Of those that used childcare, a slight majority used private arrangements,
  while a substantial minority used after-school or before-school care.
  - The fraction using before-school or after-school care appears to be lower
    than in ROC, but the results here are somewhat noisy.
  - Usage of before-school or after-school care was much higher in Quebec than
    in either BC or ROC.
- In English Canada as a whole, childcare usage was:
  - positively associated with parent's education, income, and age.
  - positively associated with mother's labour supply.
  - higher for single-parent households.
  - lower for immigrants, visible minorities, speakers of a non-official
    language at home, and the religiously observant.

Additional details are provided below. Replication files for this analysis are
available at https://github.com/bvkrauth/fdk-gss. 

<<dd_do:quietly>>
/* Setup */
run config.do
/* Get data */
use "${clean_data}/clean_gss", clear
<</dd_do>>

## Data description

<<dd_do:quietly>>
/* Count cases */
// Total number of cases in GSS
count
local n_total = r(N)
// Number of cases with childcare data
count if !missing(ccp) | !missing(ccs)
local n_childcare = r(N)
// Number of BC 5 year olds
count if PRV == 59 & !missing(cc5)
local n_bc5 = r(N)
// Number of BC 5 year olds in childcare
count if PRV == 59 & !missing(cc5_type)
local n_bc5cc = r(N)
<</dd_do>>

The 2011 GSS is a national survey of <<dd_display: %9.0gc `n_total'>>
households. Childcare information is available for the
<<dd_display: %9.0gc `n_childcare'>>
of these households that had children age 0-14.

Each observation in the data set corresponds to a household. Weights are
available at the person-level (`WGHT_PER`) and household level (`WGHT_HSD`)
and represent the number of people/households in Canada the observation
represents. Following Statistics Canada (2012), all analysis in this document
uses person weights.

~~~
<<dd_do>>
sum WGHT_PER WGHT_HSD
<</dd_do>>
~~~

The constructed childcare variables all start with "cc" and are aimed at
replicating the tables on childcare in Statistics Canada (2012).

~~~
<<dd_do: nocommands>>
preserve
describe cc* , replace clear
list name varlab, table noheader clean noobs
restore
<</dd_do>>
~~~

The variable `ccp` identifies whether or not at least one preschool-age (0-4)
child is receiving childcare.  It is missing if there is no preschool-age child
in the household.
~~~
<<dd_do>>
ta ccp
<</dd_do>>
~~~

The variable `ccp_type` identifies the type of childcare received by
preschool-age children in the household.  It is missing unless there is at least
one preschool-age child receiving regular childcare.
~~~
<<dd_do>>
ta ccp_type
<</dd_do>>
~~~

Similarly, the variable `ccs` identifies whether or not at least one school-age
(5-14) child is receiving childcare, and the variable `ccs_type` identifies the
type of childcare. Note that the coding of childcare types is different for
school-age children versus preschool-age children.
~~~
<<dd_do>>
ta ccs_type ccs, missing
<</dd_do>>
~~~

Finally, we have created similar variables for every age. Pick any age
A (I will pick A = 5 in the examples below). Then `ccA` identifies whether or
not at least one age A child is receiving childcare, and `ccA_type` identifies
the type of childcare received.
~~~
<<dd_do>>
ta cc5_type cc5, missing
<</dd_do>>
~~~

### Replications of tables from Statistics Canada

We start by replicating tables on Canada-wide childcare usage from the
Statistics Canada report (Statistics Canada 2012).

#### Table 12: Distribution of parents of preschool-aged children by use of child care arrangement, Canada, 2011

Used a child care arrangement for at least one preschool-aged child (number).
~~~
<<dd_do>>
table ccp [pw = WGHT_PER ] , stat(sumw)
<</dd_do>>
~~~

Used a child care arrangement for at least one preschool-aged child (percent).
~~~
<<dd_do>>
table ccp [pw = WGHT_PER ] , stat(percent)
<</dd_do>>
~~~

Type of child care arrangement (number).
~~~
<<dd_do>>
table ccp_type [pw = WGHT_PER ] , stat(sumw)
<</dd_do>>
~~~

Type of child care arrangement (percent).
~~~
<<dd_do>>
table ccp_type [pw = WGHT_PER ] , stat(percent)
<</dd_do>>
~~~

#### Table 14: Distribution of parents of school-aged children by use of child care arrangement, Canada, 2011

Used a child care arrangement for at least one school-aged child (number).
~~~
<<dd_do>>
table ccs [pw = WGHT_PER ] , stat(sumw)
<</dd_do>>
~~~

Used a child care arrangement for at least one school-aged child (percent).
~~~
<<dd_do>>
table ccs [pw = WGHT_PER ] , stat(percent)
<</dd_do>>
~~~

Type of child care arrangement (number).
~~~
<<dd_do>>
table ccs_type [pw = WGHT_PER ] , stat(sumw)
<</dd_do>>
~~~

Type of child care arrangement (percent).
~~~
<<dd_do>>
table ccs_type [pw = WGHT_PER ] , stat(percent)
<</dd_do>>
~~~

## Childcare for BC five year olds

Our target population is BC families with five year olds. Unfortunately,
there are only <<dd_display: %9.0gc `n_bc5'>> such households in the data,
<<dd_display: %9.0gc `n_bc5cc'>> of whom use childcare. As a result we will
supplement the direct analysis of our target population with analysis of
similar and somewhat larger comparison groups.

### Direct analysis of target population

<<dd_do:quietly>>
/* Estimate childcare usage */
svyset [pw = WGHT_PER]
// Childcare usage for BC 5 year olds
svy: tabulate cc5 if PRV == 59
matrix b = e(b)
local pct_bc5_cc = b[1,3]
// Childcare usage for Quebec 5 year olds
svy: tabulate cc5 if PRV == 24
matrix b = e(b)
local pct_qc5_cc = b[1,3]
// Childcare usage for ROC 5 year olds
svy: tabulate cc5 if !inlist(PRV,24,59)
matrix b = e(b)
local pct_roc5_cc = b[1,3]
<</dd_do>>

Direct analysis of the target population indicates that
<<dd_display: %9.0fc 100*`pct_bc5_cc'>>% of BC families regularly used
childcare for their five year old.
~~~
<<dd_do>>
svy: tabulate cc5 region , col percent nomarginals stubwidth(35) format(%6.1f)
<</dd_do>>
~~~
This is much less than the <<dd_display: %9.0fc 100*`pct_qc5_cc'>>%
rate in Quebec and somewhat less than the
<<dd_display: %9.0fc 100*`pct_roc5_cc'>>% rate in the ROC (excluding BC and
Quebec).

<<dd_do:quietly>>
/* Estimate childcare usage by type */
// BC 5 year olds
svy: tabulate cc5_type if PRV == 59
matrix b = e(b)
local pct_bc5_priv = b[1,1]
local pct_bc5_serv = b[1,2]
// Quebec 5 year olds
svy: tabulate cc5_type if PRV == 24
matrix b = e(b)
local pct_qc5_priv = b[1,1]
local pct_qc5_serv = b[1,2]
// ROC 5 year olds
svy: tabulate cc5_type if !inlist(PRV,24,59)
matrix b = e(b)
local pct_roc5_priv = b[1,1]
local pct_roc5_serv = b[1,3]
<</dd_do>>

Among BC families using childcare for their 5 year old, about
<<dd_display: %9.0fc 100*`pct_bc5_priv'>>%
used a private arrangement, and most of the rest
(<<dd_display: %-9.0fc 100*`pct_bc5_serv'>>%)
used before/after school care.
~~~
<<dd_do>>
svy: tabulate cc5_type region , col percent nomarginals stubwidth(35) format(%6.1f)
<</dd_do>>
~~~
Before/after school care was much more common in Quebec
(<<dd_display: %-9.0fc 100*`pct_qc5_serv'>>%)
and somewhat more common in the ROC
(<<dd_display: %-9.0fc 100*`pct_roc5_serv'>>%)
than in BC.

### Other age groups

As indicated in the previous section, limited data are available on BC families
with 5 year olds. As a result, we also look at slightly younger (preschool age)
and slightly older (school age) children.

#### Preschool age

<<dd_do:quietly>>
/* Estimate childcare usage */
// BC preschool-age
svyset [pw = WGHT_PER]
svy: tabulate ccp if PRV == 59
matrix b = e(b)
local pct_bcp_cc = b[1,3]
// Quebec preschool-age
svy: tabulate ccp if PRV == 24
matrix b = e(b)
local pct_qcp_cc = b[1,3]
// ROC preschool-age
svy: tabulate ccp if !inlist(PRV,24,59)
matrix b = e(b)
local pct_rocp_cc = b[1,3]
<</dd_do>>

About <<dd_display: %9.0fc 100*`pct_bcp_cc'>>% of BC families regularly
used childcare for preschool-age children, similar to the estimated
usage rate for five year olds:
~~~
<<dd_do>>
svy: tabulate ccp region , col percent nomarginals stubwidth(35) format(%6.1f)
<</dd_do>>
~~~
As with five year olds, this was much lower in BC than
in Quebec (<<dd_display: %-9.0fc 100*`pct_qcp_cc'>>%) and slightly
lower (<<dd_display: %-9.0fc 100*`pct_rocp_cc'>>%) than in ROC.


Among that used childcare, BC families were less likely to use daycare and
more likely to use licensed home daycare or other private arrangements.
~~~
<<dd_do>>
svy: tabulate ccp_type region , col percent nomarginals stubwidth(35) format(%6.1f)
<</dd_do>>
~~~

#### Six and seven year olds

Regular childcare usage rates for BC six year olds were similar to those
for five year olds, while usage for seven year olds was slightly lower.
~~~
<<dd_do>>
svy: tabulate cc6 region , col percent nomarginals stubwidth(35) format(%6.1f)
svy: tabulate cc7 region , col percent nomarginals stubwidth(35) format(%6.1f)
<</dd_do>>
~~~
As with five year olds, these rates were much lower than Quebec and slightly
lower than the ROC.

BC parents of six and seven year olds also seemed to rely more on private
arrangements relative to before/after school care, though this is a little
noisy.
~~~
<<dd_do>>
svy: tabulate cc6_type region , col percent nomarginals stubwidth(35) format(%6.1f)
svy: tabulate cc7_type region , col percent nomarginals stubwidth(35) format(%6.1f)
<</dd_do>>
~~~

#### School age

The pattern of lower usage of before/after school care in BC is also observed
in school-age children more generally, suggesting that access to such care was
more limited in BC than elsewhere.
~~~
<<dd_do>>
svy: tabulate ccs_type region , col percent nomarginals stubwidth(35) format(%6.1f)
<</dd_do>>
~~~

## Who uses childcare?

Finally, we investigate the question of *who* tended to use different types of
childcare among the parents of BC 5 year olds. The sample size for this target
population is clearly too small for such an analysis, so we investigate this
question for the full population of preschool-age and school-age children in all
of Canada excluding Quebec. Quebec is a large province with distinct
demographics and childcare institutions leading to unusually high rates of
childcare utilization. As a result, it is not a good proxy for the childcare
environment in BC.

### Residential location

#### Province of residence

Although BC had a slightly lower childcare utilization rate than other
provinces, it is not a significant outlier.
~~~
<<dd_do>>
svy : tabulate PRV ccp if PRV != 24 , row percent nomarginals cellwidth(20) stubwidth(20) format(%6.1f)
svy : tabulate PRV ccs if PRV != 24 , row percent nomarginals cellwidth(20) stubwidth(20) format(%6.1f)
<</dd_do>>
~~~

#### Urban/rural residence

Urban (CMA) residents were more likely than rural residents to use childcare.
~~~
<<dd_do>>
svy : tabulate LUC_RST ccp if PRV != 24, row percent nomarginals cellwidth(20) stubwidth(20) format(%6.1f)
svy : tabulate LUC_RST ccs if PRV != 24, row percent nomarginals cellwidth(20) stubwidth(20) format(%6.1f)
<</dd_do>>
~~~

### Household composition

Childcare usage varied by household composition, but the relationship can be
complex.

### Household size

Number of children in household was negatively related to school-aged Childcare
usage but had a nonmonotonic relationship with preschool-age childcare usage.
~~~
<<dd_do>>
svy : tabulate CHDINFTC ccp if PRV != 24, row percent nomarginals cellwidth(20) stubwidth(20) format(%6.1f)
svy : tabulate CHDINFTC ccs if PRV != 24, row percent nomarginals cellwidth(20) stubwidth(20) format(%6.1f)
<</dd_do>>
~~~

### Parental age

Older parents were more likely to use childcare for preschool-aged children, but
the relationship for school-aged children is less clear.
~~~
<<dd_do>>
svy : tabulate AGEGR5 ccp if inrange(AGEGR5,4,7) & PRV != 24, row percent nomarginals cellwidth(20) stubwidth(20) format(%6.1f)
svy : tabulate AGEGR5 ccs if inrange(AGEGR5,4,7) & PRV != 24, row percent nomarginals cellwidth(20) stubwidth(20) format(%6.1f)
<</dd_do>>
~~~

### Marital status

Partnered and married/common-law parents were less likely to use childcare
than unpartnered and single/divorced/separated parents.
~~~
<<dd_do>>
svy : tabulate partner ccp if PRV != 24, row percent nomarginals cellwidth(20) stubwidth(20) format(%6.1f)
svy : tabulate partner ccs if PRV != 24, row percent nomarginals cellwidth(20) stubwidth(20) format(%6.1f)
svy : tabulate MARSTAT ccp if PRV != 24, row percent nomarginals cellwidth(20) stubwidth(20) format(%6.1f)
svy : tabulate MARSTAT ccs if PRV != 24, row percent nomarginals cellwidth(20) stubwidth(20) format(%6.1f)
<</dd_do>>
~~~

### Economic factors

Childcare usage was higher in households with higher income and parental
education, and is also positively associated with mother's labour supply.

#### Income

Higher-income households were generally more likely to use childcare.
~~~
<<dd_do>>
svy : tabulate INCMHSDC ccp if PRV != 24, row percent nomarginals cellwidth(20) stubwidth(20) format(%6.1f)
svy : tabulate INCMHSDC ccs if PRV != 24, row percent nomarginals cellwidth(20) stubwidth(20) format(%6.1f)
<</dd_do>>
~~~

#### Education

More educated households were generally more likely to use childcare.
~~~
<<dd_do>>
svy : tabulate EDU5 ccp if PRV != 24, row percent nomarginals cellwidth(20) stubwidth(20) format(%6.1f)
svy : tabulate EDU5 ccs if PRV != 24, row percent nomarginals cellwidth(20) stubwidth(20) format(%6.1f)
<</dd_do>>
~~~

#### Mother's employment

Mother's employment is also positively related to childcare usage.
~~~
<<dd_do>>
svy : tabulate MAR_Q133 ccp if SEX == 2 & PRV != 24, row percent nomarginals cellwidth(20) stubwidth(20) format(%6.1f)
svy : tabulate MAR_Q133 ccs if SEX == 2 & PRV != 24, row percent nomarginals cellwidth(20) stubwidth(20) format(%6.1f)
<</dd_do>>
~~~

### Cultural factors

Immigrants, visible minorities, and speakers of a HL other than English or
French were less likely to use childcare, as were the religiously observant.

#### Immigration

Immigrants were less likely to use childcare than the Canadian-born.
~~~
<<dd_do>>
svy : tabulate BRTHCAN ccp if PRV != 24, row percent nomarginals cellwidth(20) stubwidth(20) format(%6.1f)
svy : tabulate BRTHCAN ccs if PRV != 24, row percent nomarginals cellwidth(20) stubwidth(20) format(%6.1f)
<</dd_do>>
~~~

#### Visible minority status

Visible minorities were less likely to use childcare.
~~~
<<dd_do>>
svy : tabulate VISMINC ccp if PRV != 24, row percent nomarginals cellwidth(20) stubwidth(20) format(%6.1f)
svy : tabulate VISMINC ccs if PRV != 24, row percent nomarginals cellwidth(20) stubwidth(20) format(%6.1f)
<</dd_do>>
~~~

#### Home language

Speakers of a home language other than English or French were less likely to use
childcare.
~~~
<<dd_do>>
svy : tabulate LANHSDC ccp if PRV != 24, row percent nomarginals cellwidth(20) stubwidth(20) format(%6.1f)
svy : tabulate LANHSDC ccs if PRV != 24, row percent nomarginals cellwidth(20) stubwidth(20) format(%6.1f)
<</dd_do>>
~~~

#### Religion

Religion matters, but not in an entirely straightforward way.  Childcare usage
was above average among Catholics, United Church and no-religion, and below
average among Protestants and "other" religions.
~~~
<<dd_do>>
svy : tabulate RELIG6C ccp if PRV != 24, row percent nomarginals cellwidth(20) stubwidth(20) format(%6.1f)
svy : tabulate RELIG6C ccs if PRV != 24, row percent nomarginals cellwidth(20) stubwidth(20) format(%6.1f)
<</dd_do>>
~~~

Religious attendance has a clear negative association with childcare usage.
~~~
<<dd_do>>
svy : tabulate RELIGATT ccp if PRV != 24, row percent nomarginals cellwidth(20) stubwidth(20) format(%6.1f)
svy : tabulate RELIGATT ccs if PRV != 24, row percent nomarginals cellwidth(20) stubwidth(20) format(%6.1f)
<</dd_do>>
~~~

Similar relationships hold with respect to religious participation and
importance.
~~~
<<dd_do>>
svy : tabulate RLR_Q110 ccp if PRV != 24, row percent nomarginals cellwidth(20) stubwidth(20) format(%6.1f)
svy : tabulate RLR_Q110 ccs if PRV != 24, row percent nomarginals cellwidth(20) stubwidth(20) format(%6.1f)
svy : tabulate RLR_Q120 ccp if PRV != 24, row percent nomarginals cellwidth(20) stubwidth(20) format(%6.1f)
svy : tabulate RLR_Q120 ccs if PRV != 24, row percent nomarginals cellwidth(20) stubwidth(20) format(%6.1f)
<</dd_do>>
~~~

## References

Friesen, Jane; Brian Krauth, and Ricardo Meilman Cohn, 2025. "Universal versus
targeted full-day Kindergarten: implications for student achievement" Working
paper, available at https://bvkrauth.github.io/publication/fdk_fsa.

Statistics Canada, 2012. "2011 General Social Survey: Overview of Families in
Canada -- Selected Tables on Families in Canada."
<https://www150.statcan.gc.ca/n1/pub/89-650-x/89-650-x2012001-eng.htm>

